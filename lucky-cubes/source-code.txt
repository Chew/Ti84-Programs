0→M
ClrHome
Disp "WELCOME TO: LUCKY CUBES"
Disp "N>NEW GAME
Disp "L>LOAD GAME
Input ">",Str4
If Str4="N"
Then
5→⌊LCGM(1)
100→⌊LCGM(2)
0→⌊LCGM(3)
startTmr→⌊LCGM(4)
End
ClrHome
Disp "WELCOME BACK
Repeat 5≠5
If M=0
Then
Repeat M≠0
Disp "ENTER OPTION
Disp "O>OPEN CUBES
Disp "B>BUY CUBES
Disp "I>ITEMS
Disp "M>MERCENARIES
Disp "S>SETTINGS
Disp "Q>QUIT
Input ">",Str4
If Str4="Q"
Then
ClrHome
Disp "SEE YOU LATER!
Stop
End
If Str4="O"
2→M
If Str4="B"
3→M
If Str4="S"
6→M
If Str4="M"
Then
ClrHome
1→M
End
If Str4="I"
5→M
If M=6
Then
ClrHome
While M=6
Disp "SETTINGS
Disp "I>GAME INFO
Disp "R>RESET
Disp "B>BACK
Input ">",Str5
If Str5="I"
Then
ClrHome
Disp "LUCKY CUBES
Disp "MADE BY: CHEWSTERCHEW
Disp "VERSION: 1.1
Disp "","","","","PRESS ENTER TO CONT."
Pause 
ClrHome
End
If Str5="B"
Then
ClrHome
0→M
End
If Str5="R"
Then
ClrHome
Disp "RESET MENU
Disp "NOTE: RESETS TO 0
Disp ""
Disp "M>RESET MERCENARIES
Disp "O>RESET COINS
Disp "C>RESET CUBES
Disp "T>RESET MERC TIMER
Input ">",Str6
If Str6="T"
Then
ClrHome
startTmr→⌊LCGM(4)
Disp "MERC TIMER RESET
End
If Str6="M"
Then
ClrHome
0→⌊LCGM(3)
Disp "MERCENARIES CLEARED
End
If Str6="O"
Then
ClrHome
0→⌊LCGM(2)
Disp "COINS CLEARED
End
If Str6="C"
Then
ClrHome
0→⌊LCGM(1)
Disp "CUBES CLEARED
End
End
End
End
If M=3
Then
ClrHome
1→A
100→B
Disp "COINS:",⌊LCGM(2)
Disp "BUY 1 CUBE FOR 100 COINS?
Repeat M≠3
Disp "Y>YES
Disp "N>NO
Disp "S>SELECT AMOUNT TO BUY
Input ">",Str5
If Str5="S"
Then
ClrHome
Repeat round(A,0)=A and A≥1
Disp "SELECT AMOUNT
Input ">",A
If round(A,0)≠A
Disp "CANNOT BE A DECIMAL
If A<1
Disp "MUST BE 1 OR MORE
End
A*100→B
ClrHome
Disp "COST TO BUY:",B
End
If Str5="Y"
Then
If ⌊LCGM(2)≥B
Then
ClrHome
⌊LCGM(2)-B→⌊LCGM(2)
⌊LCGM(1)+A→⌊LCGM(1)
Disp "PURCHASE SUCCESSFUL
0→M
Else
ClrHome
Disp "INSUFFICIENT FUNDS
0→M
End
End
If Str5="N"
Then
ClrHome
Disp "PURCHASE CANCELLED
0→M
End
End
End
If M=5
Then
Repeat M≠5
ClrHome
Disp "COINS:",⌊LCGM(2)
Disp "CUBES:",⌊LCGM(1)
Disp "MERCENARIES:",⌊LCGM(3)
Disp "PRESS ENTER TO CONT.
Pause 
0→M
ClrHome
End
End
If M=1
Then
Repeat M≠1
Disp "MERCENARY MENU
Disp "SELECT OPTION
Disp "P>PURCHASE
Disp "C>COLLECT COINS
Disp "I>INFORMATION
Disp "B>BACK
Input ">",Str5
If Str5="C"
Then
startTmr-⌊LCGM(4)→N
N/300→N
N-.5→N
round(N,0)→N
N*⌊LCGM(3)→N
ClrHome
Disp "COINS COLLECTED:",N
Disp "PRESS ENTER TO CONT."
N+⌊LCGM(2)→⌊LCGM(2)
startTmr→⌊LCGM(4)
Pause 
ClrHome
End
If Str5="P"
Then
⌊LCGM(3)*20→J
ClrHome
If J>⌊LCGM(2)
Then
ClrHome
Disp "INSUFFICIENT FUNDS
Else
Disp "COST TO BUY:",J
Disp "BUY 1 MERCENARY?
Disp "Y>YES
Disp "N>NO
Input ">",Str6
If Str6="Y"
Then
⌊LCGM(2)-J→⌊LCGM(2)
⌊LCGM(3)+1→⌊LCGM(3)
ClrHome
Disp "PURCHASE SUCCESSFUL
If ⌊LCGM(3)=1
Then
startTmr→⌊LCGM(4)
End
Else
ClrHome
Disp "PURCHASE CANCELLED
End
End
End
If Str5="B"
Then
ClrHome
0→M
End
If Str5="I"
Then
ClrHome
Disp "MERCENARIES HELP FIND","COINS, THEY FIND ONE","EVERY 5 MINUTES"
Disp "","PRESS ENTER TO CONT."
Pause 
ClrHome
End
End
End
If M=2
Then
ClrHome
If ⌊LCGM(1)=0
Then
Disp "YOU HAVE NO CUBES
0→M
Else
Repeat M≠2
Disp "CUBES:",⌊LCGM(1)
0→L
Repeat L=1
Disp "ENTER CUBES TO OPEN
Input ">",O
0→E
If O>100
Then
1→E
ClrHome
Disp "NUMBER TOO HIGH
End
If O>⌊LCGM(1)
Then
1→E
ClrHome
Disp "INSUFFICIENT CUBES
End
If O=0
Then
0→M
ClrHome
Disp "CANCELLING
End
If O<0
Then
1→E
ClrHome
Disp "NUMBER TOO LOW
End
If E=0
1→L
End
Disp "PRESS ENTER TO OPEN
Pause 
Disp "OPENING...
0→P
0→U
Repeat O=P
round(rand*50,0)→T
T+U→U
P+1→P
End
⌊LCGM(1)-O→⌊LCGM(1)
⌊LCGM(2)+U→⌊LCGM(2)
ClrHome
Disp "COINS FOUND:",U
0→M
End
End
End
End
Disp "THIS MESSAGE SHOULD","NOT APPEAR"
Stop
